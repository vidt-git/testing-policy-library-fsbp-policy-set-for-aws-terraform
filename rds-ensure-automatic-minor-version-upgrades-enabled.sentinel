# This policy requires resources of type `aws_db_instance` have attribute "auto_minor_version_upgrade" set to true.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":              "rds-ensure-automatic-minor-version-upgrades-enabled",
	"message":                  "Attribute 'auto_minor_version_upgrade' must be set to true for 'aws_db_instance' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-13 for more details.",
	"resource_aws_db_instance": "aws_db_instance",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_db_instance).resources
violations = collection.reject(resources, func(res) {
	auto_minor_upgrade_flag = "values.auto_minor_version_upgrade"
	return maps.get(res, auto_minor_upgrade_flag, false) is true
})

summary = {
	"policy_name": "rds-ensure-automatic-minor-version-upgrades-enabled",
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
