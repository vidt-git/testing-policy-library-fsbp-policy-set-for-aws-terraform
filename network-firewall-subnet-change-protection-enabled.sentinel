# This policy requires resources of type `aws_networkfirewall_firewall` have attribute "subnet_change_protection" set to true.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name": "network-firewall-subnet-change-protection-enabled",
	"message":     "Attribute 'subnet_change_protection' must be set to true for 'aws_networkfirewall_firewall' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/networkfirewall-controls.html#networkfirewall-10 for more details.",
	"resource_aws_networkfirewall_firewall": "aws_networkfirewall_firewall",
	"subnet_change_protection":              "subnet_change_protection",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_networkfirewall_firewall).resources
violations = collection.reject(resources, func(res) {
	subnet_protection = maps.get(res, "values.subnet_change_protection", false)
	if subnet_protection == null {
		return false
	}
	return subnet_protection == true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
