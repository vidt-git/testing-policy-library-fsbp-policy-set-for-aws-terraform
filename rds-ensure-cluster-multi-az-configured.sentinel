# This policy requires resources of type `aws_rds_cluster` to be configured for multiple availability zones

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":              "rds-ensure-cluster-multi-az-configured",
	"message":                  "Attribute 'availability_zones' must have two or more values for 'aws_rds_cluster' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-15 for more details.",
	"resource_aws_rds_cluster": "aws_rds_cluster",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_rds_cluster).resources
violations = collection.reject(resources, func(res) {
	availability_zones = maps.get(res, "values.availability_zones", [])
	return length(availability_zones) >= 2
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
