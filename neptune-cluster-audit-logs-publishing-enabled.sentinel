# This policy requires attribute 'enable_cloudwatch_logs_exports' to contain 'audit' for 'aws_neptune_cluster' resources

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                  "neptune-cluster-audit-logs-publishing-enabled",
	"resource_aws_neptune_cluster": "aws_neptune_cluster",
}

# Functions
get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		cloudwatch_logs_exports = maps.get(res, "values.enable_cloudwatch_logs_exports", [])
		if cloudwatch_logs_exports is null {
			return false
		}
		return cloudwatch_logs_exports contains "audit"
	})
}

# Variables

aws_aws_neptune_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_neptune_cluster).resources
violations = get_violations(aws_aws_neptune_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        "Attribute 'enable_cloudwatch_logs_exports' must contain 'audit' for 'aws_neptune_cluster' resources.Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/neptune-controls.html#neptune-2 for more details.",
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
