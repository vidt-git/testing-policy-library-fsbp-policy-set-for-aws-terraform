# This policy checks if resources of type 'aws_opensearch_domain' have the
# 'instance_count' greater than or equal to 3
# in the 'cluster_config' block

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Param

param instance_count default 3

# Constants
const = {
	"policy_name":                    "opensearch-data-node-fault-tolerance",
	"message":                        "Attribute 'instance_count' in 'cluster_config' should atleast 3 for AWS OpenSearch Domain. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/opensearch-controls.html#opensearch-6 for more details.",
	"resource_aws_opensearch_domain": "aws_opensearch_domain",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		cluster_config_values = maps.get(res, "values.cluster_config", null)
		if cluster_config_values is null or cluster_config_values is empty {
			return false
		}
		return maps.get(cluster_config_values[0], "instance_count", 1) >= instance_count
	})
}

# Variables

opensearch_domain_resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_opensearch_domain).resources
violations = get_violations(opensearch_domain_resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
