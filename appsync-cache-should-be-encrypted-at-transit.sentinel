# This policy requires resources of type `aws_appsync_api_cache` have attribute `transit_encryption_enabled` should be `true`.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                    "appsync-cache-should-be-encrypted-at-transit",
	"message":                        "Attribute 'transit_encryption_enabled' must be set to 'true' for 'aws_appsync_api_cache' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/appsync-controls.html#appsync-6 for more details.",
	"resource_aws_appsync_api_cache": "aws_appsync_api_cache",
	"transit_encryption_enabled":     "transit_encryption_enabled",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_appsync_api_cache).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res.values, const.transit_encryption_enabled, false) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
