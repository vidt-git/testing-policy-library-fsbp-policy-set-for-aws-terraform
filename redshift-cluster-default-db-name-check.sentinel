# This policy checks if resources of type 'aws_redshift_cluster' have the 'database_name'
# attribute not set to 'dev' (default value)

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                   "redshift-cluster-default-db-name-check",
	"message":                       "Attribute 'database_name' should not be 'dev' (default value) for AWS Redshift Cluster. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/redshift-controls.html#redshift-9 for more details.",
	"resource_aws_redshift_cluster": "aws_redshift_cluster",
	"database_name_default_value":   "dev",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.database_name", const.database_name_default_value) is not const.database_name_default_value
	})
}

# Variables

redshift_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_redshift_cluster).resources
violations = get_violations(redshift_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
