# This control checks whether 'aws_guardduty_detector_feature' EKS Runtime Monitoring with automated agent management is enabled.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                             "guardduty-eks-protection-runtime-should-be-enabled",
	"resource_aws_guardduty_detector_feature": "aws_guardduty_detector_feature",
	"message": "'aws_guardduty_detector_feature' EKS Runtime Monitoring with automated agent management should be enabled. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/guardduty-controls.html#guardduty-7 for more details.",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		name = maps.get(res.values, "name", "")
		status = maps.get(res.values, "status", "")
		return name is "EKS_RUNTIME_MONITORING" and status is "ENABLED"
	})
}

# Variables

aws_guardduty_detector_feature = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_guardduty_detector_feature).resources
violations = get_violations(aws_guardduty_detector_feature)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
