# This policy requires resources of type `aws_db_instance` to have `monitoring_interval` set to any value in (1, 5, 10, 15, 30, 60)

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":              "rds-ensure-monitoring-configured",
	"message":                  "Attribute 'monitoring_interval' must be set to any value in (1, 5, 10, 15, 30, 60) for 'aws_db_instance' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-6 for more details.",
	"resource_aws_db_instance": "aws_db_instance",
	"monitoring_interval":      "monitoring_interval",
	"allowed_custom_values":    [1, 5, 10, 15, 30, 60],
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_db_instance).resources
violations = collection.reject(resources, func(res) {
	monitoring_interval = "values.monitoring_interval"
	return maps.get(res, monitoring_interval, 0) in const.allowed_custom_values
})

summary = {
	"policy_name": "rds-ensure-monitoring-configured",
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
