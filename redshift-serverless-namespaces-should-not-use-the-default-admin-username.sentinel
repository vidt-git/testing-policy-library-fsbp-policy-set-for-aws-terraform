# This control checks whether the admin username for an Amazon Redshift Serverless namespace is the default admin username, admin.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfconfig/v2" as tfconfig
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name": "redshift-serverless-default-admin-check",
	"message":     "Redshift Serverless namespaces should not use the default admin username 'admin'. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/redshiftserverless-controls.html#redshiftserverless-5 for more details.",
	"resource_aws_redshiftserverless_namespace": "aws_redshiftserverless_namespace",
	"default_admin_username":                    "admin",
}

# Variables

resources = tf.config(tfconfig.resources).type(const.resource_aws_redshiftserverless_namespace).resources
violations = collection.reject(resources, func(res) {
	config = maps.get(res, "config", {})
	if config not contains "admin_username" {
		return false
	}
	admin_username = maps.get(config, "admin_username.constant_value", const.default_admin_username)
	if admin_username == "" {
		return false
	}
	return admin_username != const.default_admin_username
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
