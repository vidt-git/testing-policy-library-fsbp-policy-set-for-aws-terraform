# This policy requires resources of type `aws_fsx_openzfs_file_system` have the `copy_tags_to_backups` and 'copy_tags_to_columes' attributes set to true.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Import

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name": "fsx-openzfs-copy-tags-to-backups-and-volumes-enabled",
	"message":     "Attributes 'copy_tags_to_backups' and 'copy_tags_to_volumes' must be true for 'aws_fsx_openzfs_file_system' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/fsx-controls.html#fsx-1 for more details.",
	"resource_aws_fsx_openzfs_file_system": "aws_fsx_openzfs_file_system",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.copy_tags_to_backups", false) is true and maps.get(res, "values.copy_tags_to_volumes", false)
	})
}

# Variables

fsx_openzfs_file_system_resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_fsx_openzfs_file_system).resources
violations = get_violations(fsx_openzfs_file_system_resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
