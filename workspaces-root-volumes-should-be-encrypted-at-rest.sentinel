# This control checks whether a root volume in an Amazon WorkSpaces WorkSpace is encrypted at rest.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                       "workspaces-root-volumes-should-be-encrypted-at-rest",
	"message":                           "Attribute 'root_volume_encryption_enabled' must be set to true for 'aws_workspaces_workspace' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/workspaces-controls.html#workspaces-2 for more details.",
	"resource_aws_workspaces_workspace": "aws_workspaces_workspace",
	"root_volume_encryption_enabled":    "root_volume_encryption_enabled",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_workspaces_workspace).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res, "values.root_volume_encryption_enabled", false) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
