# This policy requires resources of type `aws_db_instance` and `aws_rds_cluster` to have `iam_database_authentication_enabled` set to true

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Params

param resource_type default "aws_db_instance"

# Constants

const = {
	"policy_name":              "rds-ensure-db-instance-have-iam-auth-configured",
	"db_instance_engine_types": ["mysql", "postgres", "aurora", "aurora-mysql", "aurora-postgresql", "mariadb"],
	"aws_db_instance":          "aws_db_instance",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(resource_type).resources
violations = collection.reject(resources, func(res) {
	if resource_type == const.aws_db_instance {
		engine_type = maps.get(res, "values.engine", "")
		if engine_type not in const.db_instance_engine_types {
			return true
		}
	}
	iam_database_authentication_enabled = "values.iam_database_authentication_enabled"
	return maps.get(res, iam_database_authentication_enabled, null) is true
})

message = "Attribute 'iam_database_authentication_enabled' must be set to true for 'aws_rds_cluster' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-12 for more details."
if resource_type == const.aws_db_instance {
	message = "Attribute 'iam_database_authentication_enabled' must be set to true for 'aws_db_instance' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-10 for more details."
}

summary = {
	"policy_name": "rds-ensure-db-instance-have-iam-auth-configured",
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	resource_type in ["aws_db_instance", "aws_rds_cluster"] and violations is empty
}
