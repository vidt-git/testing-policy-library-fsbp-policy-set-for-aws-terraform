# This policy checks if resources of type 'aws_lb' has the
# 'enable_deletion_protection' attribute set to true.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":     "elb-ensure-deletion-protection-enabled",
	"message":         "Application, Gateway, and Network Load Balancers should have deletion protection enabled. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/elb-controls.html#elb-6 for more details.",
	"resource_aws_lb": "aws_lb",
}

# Variables

aws_load_balancers = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_lb).resources

invalid_load_balancers = collection.reject(aws_load_balancers, func(res) {
	return maps.get(res, "values.enable_deletion_protection", false) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map invalid_load_balancers as _, res {
		{
			"address":        res.address,
			"module_address": res.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	invalid_load_balancers is empty
}
