# This policy requires resources of type `aws_efs_file_system` have attribute "encrypted" set to true.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                  "efs-filesystem-encrypted",
	"message":                      "Attribute 'encrypted' must be set to true for 'aws_efs_file_system' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/efs-controls.html#efs-8 for more details.",
	"resource_aws_efs_file_system": "aws_efs_file_system",
	"encrypted":                    "encrypted",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_efs_file_system).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res, "values.encrypted", false) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
