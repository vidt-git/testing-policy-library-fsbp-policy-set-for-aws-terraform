# This policy checks if resources of type 'aws_opensearch_domain' have the 'enable' attribute set to true and 'log_type' set to 'AUDIT_LOGS'
# in the 'log_publishing_options' block

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                    "opensearch-audit-logging-enabled",
	"message":                        "Attribute 'enable' in 'log_publishing_options' should be true and 'log_type' set to 'AUDIT_LOGS' for AWS OpenSearch Domain. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/opensearch-controls.html#opensearch-5 for more details.",
	"resource_aws_opensearch_domain": "aws_opensearch_domain",
	"enabled":                        "enabled",
	"log_type":                       "log_type",
	"AUDIT_LOGS":                     "AUDIT_LOGS",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		log_publishing_options_values = maps.get(res, "values.log_publishing_options", null)
		if log_publishing_options_values is null or log_publishing_options_values is empty {
			return false
		}
		return collection.reject(log_publishing_options_values, func(log_publishing_options_value) {
			return maps.get(log_publishing_options_value, const.enabled, false) is true and maps.get(log_publishing_options_value, const.log_type, "") is const.AUDIT_LOGS
		}) is empty
	})
}

# Variables

opensearch_domain_resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_opensearch_domain).resources
violations = get_violations(opensearch_domain_resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
