# This policy requires resources of type `aws_db_instance` to have `multi_az` set to true

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":              "rds-ensure-multi-az-configuration",
	"message":                  "Attribute 'multi_az' must be set to true for 'aws_db_instance' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-5 for more details.",
	"resource_aws_db_instance": "aws_db_instance",
	"multi_az":                 "multi_az",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_db_instance).resources
violations = collection.reject(resources, func(res) {
	multi_az = "values.multi_az"
	return maps.get(res, multi_az, false) is true
})

summary = {
	"policy_name": "rds-ensure-multi-az-configuration",
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
