# This policy checks if resources of type 'aws_rds_cluster' have the 'storage_encrypted'
# attribute set to true

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":              "rds-cluster-encrypted-at-rest",
	"message":                  "Attribute 'storage_encrypted' should be true for AWS RDS Cluster. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-27 for more details.",
	"resource_aws_rds_cluster": "aws_rds_cluster",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.storage_encrypted", null) is true
	})
}

# Variables

rds_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_rds_cluster).resources
violations = get_violations(rds_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
