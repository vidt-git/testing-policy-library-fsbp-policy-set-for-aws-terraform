# This control checks whether an 'aws_cloudfront_distribution' is configured with an origin group that has two or more origins.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                          "cloudfront-distributions-should-have-origin-failover-configured",
	"resource_aws_cloudfront_distribution": "aws_cloudfront_distribution",
	"message": "'aws_cloudfront_distribution' is configured with an origin group that has two or more origins. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/cloudfront-controls.html#cloudfront-4 for more details.",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		origin = maps.get(res.values, "origin", [])
		if length(origin) < 2 {
			return true
		}
		origin_group = maps.get(res.values, "origin_group", [])
		return origin_group is not empty
	})
}

# Variables

aws_cloudfront_distribution = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_cloudfront_distribution).resources
violations = get_violations(aws_cloudfront_distribution)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
