# This policy requires that the `aws_dax_cluster` resource has `cluster_endpoint_encryption_type` attribute set to `TLS`

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":              "dynamo-db-accelerator-clusters-encryption-in-transit-enabled",
	"resource_aws_dax_cluster": "aws_dax_cluster",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.cluster_endpoint_encryption_type", "") == "TLS"
	})
}

# Variables

aws_dax_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_dax_cluster).resources
violations = get_violations(aws_dax_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        "Attribute 'cluster_endpoint_encryption_type' must be set to 'TLS' for 'aws_dax_cluster' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/dynamodb-controls.html#dynamodb-7 for more details.",
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
