# This policy checks if resources of type 'aws_iam_policy_document'
# contain blocked action patterns such as 'kms:ReEncryptFrom' and 'kms:Decrypt'

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfstate/v2" as tfstate
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                      "kms-restrict-iam-inline-policies-decrypt-all-kms-keys",
	"message":                          "Actions 'kms:ReEncryptFrom' and 'kms:Decrypt' must not be allowed on all 'KMS keys'. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/kms-controls.html#kms-2 for more details.",
	"resource_aws_iam_policy_document": "aws_iam_policy_document",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		statements = maps.get(res, "values.statement", [])
		for statements as statement {
			actions = maps.get(statement, "actions", [])
			if actions contains "kms:ReEncryptFrom" or actions contains "kms:Decrypt" {
				return false
			}
		}
		return true
	})
}

# Variables

policy_document = tf.state(tfstate.resources).type(const.resource_aws_iam_policy_document).resources
violations = get_violations(policy_document)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
