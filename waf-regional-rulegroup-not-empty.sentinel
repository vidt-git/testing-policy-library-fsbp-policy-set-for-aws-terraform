# This policy checks whether an AWS WAF Regional rule group has at least one rule.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                         "waf-regional-rulegroup-not-empty",
	"resource_aws_wafregional_rule_group": "aws_wafregional_rule_group",
	"message":        "AWS WAF Regional rule group should have at least one rule. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/waf-controls.html#waf-3 for more details.",
	"activated_rule": "activated_rule",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		rules = maps.get(res.values, const.activated_rule, [])
		return rules is not null and length(rules) > 0
	})
}

# Variables

aws_wafregional_rule_group = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_wafregional_rule_group).resources
violations = get_violations(aws_wafregional_rule_group)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
