# This policy requires resources of type `aws_wafv2_web_acl` to have at least one rule or rule group.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                "wafv2-webacl-not-empty",
	"message":                    "AWS WAF web ACLs should have at least one rule or rule group. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/waf-controls.html#waf-10 for more details.",
	"resource_aws_wafv2_web_acl": "aws_wafv2_web_acl",
	"rule": "rule",
}

# Functions

has_rule_or_rule_group = func(res) {
	rules = maps.get(res.values, const.rule, [])
	return length(rules) > 0
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_wafv2_web_acl).resources
violations = collection.reject(resources, func(res) {
	return has_rule_or_rule_group(res)
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
