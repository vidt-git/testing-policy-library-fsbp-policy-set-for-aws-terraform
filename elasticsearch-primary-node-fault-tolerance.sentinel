# This policy requires resources of type `aws_elasticsearch_domain` have the `dedicated_master_count` atleast 3 and 'dedicated_master_enabled' set to true for `cluster_config` attribute.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Import

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Params
param master_count_value default 3

# Constants
const = {
	"policy_name":                       "elasticsearch-primary-node-fault-tolerance",
	"message":                           "Attribute 'dedicated_master_count' must be atleast 3 and 'dedicated_master_enabled' set to true for the attribute 'cluster_config' for 'aws_elasticsearch_domain' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/es-controls.html#es-7 for more details.",
	"resource_aws_elasticsearch_domain": "aws_elasticsearch_domain",
	"dedicated_master_count":            "dedicated_master_count",
	"dedicated_master_enabled":          "dedicated_master_enabled",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		cluster_config_values = maps.get(res, "values.cluster_config", [])
		if cluster_config_values is empty {
			return false
		}
		dedicated_master_count_value = maps.get(cluster_config_values[0], const.dedicated_master_count, 0)
		dedicated_master_enabled_value = maps.get(cluster_config_values[0], const.dedicated_master_enabled, false)
		if dedicated_master_count_value is null {
			return false
		}
		return dedicated_master_enabled_value is true and dedicated_master_count_value >= master_count_value
	})
}

# Variables

elasticsearch_resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_elasticsearch_domain).resources
violations = get_violations(elasticsearch_resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
