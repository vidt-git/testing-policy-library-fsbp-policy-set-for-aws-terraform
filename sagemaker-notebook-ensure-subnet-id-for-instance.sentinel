# This policy checks if resources of type 'aws_sagemaker_instance'
# is launched in custom vpc

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfconfig/v2" as tfconfig
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps
import "strings"

# Constants
const = {
	"policy_name": "sagemaker-notebook-ensure-subnet-id-for-instance",
	"message":     "Attribute 'subnet_id' should be launched in custom vpc for AWS Sagemaker Notebook Instance. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/sagemaker-controls.html#sagemaker-2 for more details.",
	"resource_aws_sagemaker_notebook_instance":           "aws_sagemaker_notebook_instance",
	"resource_aws_subnet":                                "aws_subnet",
	"resource_aws_vpc":                                   "aws_vpc",
	"resource_aws_default_subnet":                        "aws_default_subnet",
	"resource_aws_default_vpc":                           "aws_default_vpc",
	"regex_pattern_to_match_the_default_subnet_resource": "^(aws_default_subnet)\\.[^\\.]+",
	"regex_pattern_to_match_the_default_vpc_resource":    "^(aws_default_vpc)\\.[^\\.]+",
	"address":        "address",
	"module_prefix":  "module.",
	"module_address": "module_address",
}

# Functions

get_violations = func(resources, config_resources) {
	return collection.reject(resources, func(res) {
		aws_sagemaker_mapped_subnet_resource = maps.get(res, "config.subnet_id", "")

		# Check if the sagemaker notebook is referenced to a subnet
		if aws_sagemaker_mapped_subnet_resource is empty {
			return false
		}

		# Check if the subnet is referenced to constant value
		if maps.has(aws_sagemaker_mapped_subnet_resource, "constant_value") {
			return false
		}

		# Check if the sagemaker is mapped to default subnet
		aws_sagemaker_mapped_to_subnet_address = aws_sagemaker_mapped_subnet_resource.references[-1]
		if aws_sagemaker_mapped_to_subnet_address matches const.regex_pattern_to_match_the_default_subnet_resource {
			return false
		}

		# Extract the vpc resources which are mapped to subnet
		aws_subnet_resources = config_resources.type(const.resource_aws_subnet).resources
		is_aws_vpc_mapped_to_subnet_address = false
		aws_vpc_mapped_to_subnet_address = ""
		for aws_subnet_resources as aws_subnet_resource {
			aws_subnet_resource_without_module = resource_address_without_module_address(aws_subnet_resource)
			if aws_sagemaker_mapped_to_subnet_address == aws_subnet_resource_without_module {
				is_aws_vpc_mapped_to_subnet_address = true
				aws_vpc_mapped_to_subnet_address = aws_subnet_resource.config.vpc_id.references[-1]
			}
		}
		if is_aws_vpc_mapped_to_subnet_address is false {
			return false
		}

		# Check if subnet is mapped to default vpc
		return aws_vpc_mapped_to_subnet_address not matches const.regex_pattern_to_match_the_default_vpc_resource
	})
}

# Removes module address prefix from a resource
# and returns back the localized address for a module.
resource_address_without_module_address = func(res) {
	resource_addr = res[const.address]
	# Check for root module
	if not strings.has_prefix(resource_addr, const.module_prefix) {
		return resource_addr
	}

	module_addr_prefix = res[const.module_address] + "."
	return strings.trim_prefix(resource_addr, module_addr_prefix)
}

# Variables

config_resources = tf.config(tfconfig.resources)
sagemaker_notebooks = config_resources.type(const.resource_aws_sagemaker_notebook_instance).resources
violations = get_violations(sagemaker_notebooks, config_resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
