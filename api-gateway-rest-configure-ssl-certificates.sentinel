# This policy checks whether 'aws_api_gateway_stage' have SSL certificates configured.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                    "api-gateway-rest-configure-ssl-certificates",
	"resource_aws_api_gateway_stage": "aws_api_gateway_stage",
	"message":                        "SSL certificates should be configured for resource 'aws_api_gateway_stage' for backend authentication. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/apigateway-controls.html#apigateway-2 for more details.",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		client_certificate_id = maps.get(res, "values.client_certificate_id", null)
		return client_certificate_id is not null
	})
}

# Variables

aws_api_gateway_stage = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_api_gateway_stage).resources
violations = get_violations(aws_api_gateway_stage)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
