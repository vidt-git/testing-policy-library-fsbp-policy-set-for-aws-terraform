# This policy ensures that Application, Network, and Gateway Load Balancers span multiple Availability Zones

#  Copyright IBM Corp. 2024, 2025
#  SPDX-License-Identifier: BUSL-1.1

# Imports
import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":     "elb-multiple-az",
	"message":         "Load balancers should span multiple Availability Zones for high availability. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/elb-controls.html#elb-13 for more details.",
	"resource_aws_lb": "aws_lb",
}

lb_resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_lb).resources

violations = collection.reject(lb_resources, func(res) {
	subnets = maps.get(res.values, "subnets", [])
	return length(subnets) > 1
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, res {
		{
			"address":        res.address,
			"module_address": res.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
