# This policy checks if resources of type 'aws_redshift_cluster' have the 'master_username'
# attribute not set to null or 'awsuser' (default value)

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                   "redshift-cluster-default-admin-check",
	"message":                       "Attribute 'master_username' should not be null or 'awsuser' (default value) for AWS Redshift Cluster. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/redshift-controls.html#redshift-8 for more details.",
	"resource_aws_redshift_cluster": "aws_redshift_cluster",
	"master_username_default_value": "awsuser",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		redshift_cluster_master_username_value = maps.get(res, "values.master_username", const.master_username_default_value)
		return redshift_cluster_master_username_value is not null and redshift_cluster_master_username_value is not const.master_username_default_value
	})
}

# Variables

redshift_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_redshift_cluster).resources
violations = get_violations(redshift_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
