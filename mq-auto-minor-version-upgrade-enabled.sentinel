# This policy checks if resources of type 'aws_mq_broker' have the 'auto_minor_version_upgrade'
# attribute set to true

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":            "mq-auto-minor-version-upgrade-enabled",
	"message":                "Attribute 'auto_minor_version_upgrade' should be true for AWS MQ Broker. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/mq-controls.html#mq-3 for more details.",
	"resource_aws_mq_broker": "aws_mq_broker",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.auto_minor_version_upgrade", false) is true
	})
}

# Variables

mq_brokers = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_mq_broker).resources
violations = get_violations(mq_brokers)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
