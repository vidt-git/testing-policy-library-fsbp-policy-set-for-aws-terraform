# This policy requires resources of type `aws_kinesis_stream` have attribute "retention_period" set to at least 168 hours (7 days).

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                 "kinesis-stream-backup-retention-check",
	"message":                     "Attribute 'retention_period' must be set to at least 168 hours (7 days) for 'aws_kinesis_stream' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/kinesis-controls.html#kinesis-3 for more details.",
	"resource_aws_kinesis_stream": "aws_kinesis_stream",
	"retention_period":            "retention_period",
	"min_retention_period":        168,
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_kinesis_stream).resources
violations = collection.reject(resources, func(res) {
	retention_period = maps.get(res, "values.retention_period", 24)
	return retention_period >= const.min_retention_period
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
