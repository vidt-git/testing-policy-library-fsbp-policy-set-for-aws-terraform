# This control checks whether an 'aws_codebuild_project' Bitbucket source repository URL contains personal access tokens or a user name and password.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                    "codebuild-bitbucket-url-should-not-contain-sensitive-credentials",
	"resource_aws_codebuild_project": "aws_codebuild_project",
	"message":                        "In 'aws_codebuild_project', Bitbucket source repository URL should not contain personal access tokens or a user name and password. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/codebuild-controls.html#codebuild-1 for more details.",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		source = maps.get(res.values, "source", [])
		location = maps.get(source[0], "location", "")
		if location matches "https://[^:]+:[^@]+@" {
			return false
		}
		return true
	})
}

# Variables

aws_codebuild_project = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_codebuild_project).resources
violations = get_violations(aws_codebuild_project)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
