# This policy requires that  `server_side_encryption` attribute of the `aws_dax_cluster` resource has `enabled` set to true

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":              "dynamo-db-accelerator-clusters-encryption-at-rest-enabled",
	"resource_aws_dax_cluster": "aws_dax_cluster",
}

# Functions
get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		server_side_encryption = maps.get(res, "values.server_side_encryption", [])
		return server_side_encryption is not empty and maps.get(server_side_encryption[0], "enabled", false) is true
	})
}

# Variables

aws_dax_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_dax_cluster).resources
violations = get_violations(aws_dax_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        "Attribute 'server_side_encryption' must have 'enabled' set to true for 'aws_dax_cluster' resources.Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/dynamodb-controls.html#dynamodb-3 for more details.",
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
