# This policy requires that  `point_in_time_recovery` attribute of the `aws_dynamodb_table` resource has `enabled` set to true

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                 "dynamo-db-tables-point-in-time-recovery-enabled",
	"resource_aws_dynamodb_table": "aws_dynamodb_table",
}

# Functions
get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		point_in_time_recovery = maps.get(res, "values.point_in_time_recovery", [])
		return point_in_time_recovery is not empty and maps.get(point_in_time_recovery[0], "enabled", false) is true
	})
}

# Variables

aws_dynamodb_tables = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_dynamodb_table).resources
violations = get_violations(aws_dynamodb_tables)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        "Attribute 'point_in_time_recovery' must have 'enabled' set to true for 'aws_dynamodb_table' resources.Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/dynamodb-controls.html#dynamodb-2 for more details.",
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
