# This policy requires that  `dynamo-db-tables-delete-protection-enabled` attribute of the `aws_dynamodb_table` resource is set to true

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                 "dynamo-db-tables-delete-protection-enabled",
	"resource_aws_dynamodb_table": "aws_dynamodb_table",
}

# Functions
get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.deletion_protection_enabled", false) is true
	})
}

# Variables

aws_dynamodb_tables = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_dynamodb_table).resources
violations = get_violations(aws_dynamodb_tables)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        "Attribute 'deletion_protection_enabled' must be set to true for 'aws_dynamodb_table' resources.Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/dynamodb-controls.html#dynamodb-6 for more details.",
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
