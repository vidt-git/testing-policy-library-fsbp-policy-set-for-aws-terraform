# This policy requires resources of type `aws_launch_configuration` should have 'associate_public_ip_address' set to false.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                       "autoscaling-launch-config-public-ip-disabled",
	"message":                           "Resource Autoscaling Launch Configuration should have 'associate_public_ip_address' set to false. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/autoscaling-controls.html#autoscaling-5 for more details.",
	"resource_aws_launch_configuration": "aws_launch_configuration",
	"associate_public_ip_address":       "associate_public_ip_address",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_launch_configuration).resources

violations = collection.reject(resources, func(res) {
	return maps.get(res, "values." + const.associate_public_ip_address, false) is false

})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
