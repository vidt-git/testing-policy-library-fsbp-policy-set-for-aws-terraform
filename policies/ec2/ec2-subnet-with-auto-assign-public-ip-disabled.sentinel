// This policy requires `aws_subnet` resources to have attribute 'map_public_ip_on_launch' to be false.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

// Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

// Constants

const = {
	"policy_name":             "ec2-ebs-snapshot-public-restorable-check-account-level",
	"message":                 "Attribute 'map_public_ip_on_launch' must be false for 'aws_subnet' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/ec2-controls.html#ec2-15 for more details.",
	"resource_aws_subnet":     "aws_subnet",
	"map_public_ip_on_launch": "map_public_ip_on_launch",
}

// Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_subnet).resources

violations = collection.reject(resources, func(res) {
	return maps.get(res, "values." + const.map_public_ip_on_launch, false) is false
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

// Outputs

print(report.generate_policy_report(summary))

// Rules

main = rule {
	violations is empty
}
