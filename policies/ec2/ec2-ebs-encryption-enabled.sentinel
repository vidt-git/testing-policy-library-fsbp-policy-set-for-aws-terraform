# This policy requires resources of type `aws_ebs_volume` have the `encrypted` attribute set to `true`.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Import

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name": "ec2-ebs-encryption-enabled",
	"message":     "Attribute 'encrypted' must be set to true for 'aws_ebs_volume' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/ec2-controls.html#ec2-7 for more details.",
	"encrypted":   "encrypted",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type("aws_ebs_volume").resources
violations = collection.reject(resources, func(res) {
	encrypted_key = maps.get(res, "values.encrypted", false)
	return encrypted_key is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
