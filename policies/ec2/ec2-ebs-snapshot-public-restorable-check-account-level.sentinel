// This policy requires `aws_ebs_snapshot_block_public_access` resources to have attribute state to either 'block-new-sharing' or 'block-all-sharing'.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

// Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

// Constants

const = {
	"policy_name": "ec2-ebs-snapshot-public-restorable-check-account-level",
	"message":     "Attribute 'state' must be either be 'block-all-sharing' or 'block-new-sharing' for 'aws_ebs_snapshot_block_public_access' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/ec2-controls.html#ec2-1 for more details.",
	"resource_aws_ebs_snapshot_block_public_access": "aws_ebs_snapshot_block_public_access",
	"state":     "state",
	"unblocked": "unblocked",
}

// Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_ebs_snapshot_block_public_access).resources

violations = collection.reject(resources, func(res) {
	return maps.get(res, "values." + const.state, "unblocked") is not const.unblocked
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

// Outputs

print(report.generate_policy_report(summary))

// Rules

main = rule {
	violations is empty
}
