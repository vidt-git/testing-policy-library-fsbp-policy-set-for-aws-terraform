# This policy requires resources of type `aws_instance` to not have a public IP address.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                 "ec2-instance-should-not-have-public-ip",
	"message":                     "EC2 instances should not have a public IP address. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/ec2-controls.html#ec2-9 for more details.",
	"resource_aws_instance":       "aws_instance",
	"associate_public_ip_address": "associate_public_ip_address",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_instance).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res, "values.associate_public_ip_address", true) is false
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
