# This policy requires that the elasticbeanstalk environment have enhanced health reporting enabled

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                                "elasticbeanstalk-enhanced-health-reporting-enabled",
	"resource_aws_elastic_beanstalk_environment": "aws_elastic_beanstalk_environment",
}

# Functions
get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		settings = maps.get(res.values, "setting", [])
		return collection.find(settings, func(setting) {
			return setting.namespace is "aws:elasticbeanstalk:healthreporting:system" and
				setting.name is "SystemType" and
				setting.value is "enhanced"
		}) is defined
	})
}

# Variables
resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_elastic_beanstalk_environment).resources
violations = get_violations(resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        "Elastic Beanstalk environment does not have enhanced health reporting enabled. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/elasticbeanstalk-controls.html#elasticbeanstalk-1 for more details.",
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

main = rule {
	violations is empty
}
