# This policy checks whether an 'aws_wafregional_rule' has at least one condition.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                   "waf-regional-rule-not-empty",
	"message":                       "AWS WAF Regional rule should have at least one condition. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/waf-controls.html#waf-2 for more details.",
	"resource_aws_wafregional_rule": "aws_wafregional_rule",
	"predicate":                     "predicate",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		rules = maps.get(res.values, const.predicate, [])
		return rules is not null and length(rules) > 0
	})
}

# Variables

aws_wafregional_rule = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_wafregional_rule).resources
violations = get_violations(aws_wafregional_rule)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
