# This policy checks if resources of type 'aws_dms_replication_instance' have the 'publicly_accessible'
# attribute set to false

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name": "dms-replication-instances-should-not-be-public",
	"message":     "Attribute 'publicly_accessible' should be false for AWS DMS Replication Instance. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/dms-controls.html#dms-1 for more details.",
	"resource_aws_dms_replication_instance": "aws_dms_replication_instance",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.publicly_accessible", false) is false
	})
}

# Variables

dms_replication_instances = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_dms_replication_instance).resources
violations = get_violations(dms_replication_instances)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
