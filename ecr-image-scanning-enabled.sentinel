# This policy checks if resources of type 'aws_ecr_repository' have
# image scanning configuration enabled

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                 "ecr-image-scanning-enabled",
	"message":                     "ECR private repositories should have image scanning configured. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/ecr-controls.html#ecr-1 for more details.",
	"resource_aws_ecr_repository": "aws_ecr_repository",
	"scan_on_push":                "scan_on_push",
}

# Variables

aws_ecr_repos = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_ecr_repository).resources

ecr_repos_without_scanning_enabled = collection.reject(aws_ecr_repos, func(res) {
	scanning_config = maps.get(res, "values.image_scanning_configuration", [])
	if scanning_config is empty {
		return false
	}

	return maps.get(scanning_config[0], const.scan_on_push, false) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map ecr_repos_without_scanning_enabled as _, res {
		{
			"address":        res.address,
			"module_address": res.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	ecr_repos_without_scanning_enabled is empty
}
