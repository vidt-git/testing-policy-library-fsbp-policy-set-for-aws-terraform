# This policy checks if resources of type 'aws_rds_cluster' have the 'master_username'
# attribute not set to 'admin' (default value)

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                   "rds-cluster-default-admin-check",
	"message":                       "Attribute 'master_username' should not be 'admin' (default value) for AWS RDS Cluster. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-24 for more details.",
	"resource_aws_rds_cluster":      "aws_rds_cluster",
	"master_username_default_value": "admin",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.master_username", const.master_username_default_value) is not const.master_username_default_value
	})
}

# Variables

rds_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_rds_cluster).resources
violations = get_violations(rds_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
