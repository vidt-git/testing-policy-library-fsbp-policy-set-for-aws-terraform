# This policy checks if resources of type 'aws_rds_cluster' with 'aurora-mysql' engine
# have the 'enabled_cloudwatch_logs_exports' attribute contains 'audit'

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":              "rds-aurora-mysql-audit-logging-enabled",
	"message":                  "Attribute 'enabled_cloudwatch_logs_exports' should contain 'audit' for AWS RDS Aurora MySQL Cluster. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-34 for more details.",
	"resource_aws_rds_cluster": "aws_rds_cluster",
	"aurora_mysql_engine":      "aurora-mysql",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		if maps.get(res, "values.engine", "") is not const.aurora_mysql_engine {
			return true
		}
		cloudwatch_logs_values = maps.get(res, "values.enabled_cloudwatch_logs_exports", [])
		return cloudwatch_logs_values is not null and cloudwatch_logs_values is not empty and cloudwatch_logs_values contains "audit"
	})
}

# Variables

rds_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_rds_cluster).resources
violations = get_violations(rds_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
