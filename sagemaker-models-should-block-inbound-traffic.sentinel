# This control checks whether an Amazon SageMaker AI hosted model blocks inbound network traffic.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                  "sagemaker-models-should-block-inbound-traffic",
	"message":                      "Attribute 'enable_network_isolation' must be set to true for 'aws_sagemaker_model' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/sagemaker-controls.html#sagemaker-5 for more details.",
	"resource_aws_sagemaker_model": "aws_sagemaker_model",
	"enable_network_isolation":     "enable_network_isolation",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_sagemaker_model).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res, "values.enable_network_isolation", false) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
