# This policy checks if resources of type 'aws_docdb_cluster' have the 'backup-retention-period'
# attribute set in between '7 to 35'

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Params

param backup_retention_period_lower_limit default 7
param backup_retention_period_upper_limit default 35

# Constants

const = {
	"policy_name":                "docdb-cluster-backup-retention-check",
	"resource_aws_docdb_cluster": "aws_docdb_cluster",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		backup_retention_period_value = maps.get(res, "values.backup_retention_period", 1)
		return backup_retention_period_value >= backup_retention_period_lower_limit and backup_retention_period_value <= backup_retention_period_upper_limit
	})
}

# Variables

documentdb_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_docdb_cluster).resources
violations = get_violations(documentdb_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        "Attribute 'backup_retention_period' should be in between '" + string(backup_retention_period_lower_limit) + " to " + string(backup_retention_period_upper_limit) + "' for AWS DocumentDb Cluster. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/documentdb-controls.html#documentdb-2 for more details.",
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

check_if_backup_retention_limits_are_valid = rule {
	backup_retention_period_lower_limit < backup_retention_period_upper_limit
}

check_docdb_cluster_backup_retention_violations = rule {
	violations is empty
}

main = rule {
	check_if_backup_retention_limits_are_valid and
	check_docdb_cluster_backup_retention_violations
}
