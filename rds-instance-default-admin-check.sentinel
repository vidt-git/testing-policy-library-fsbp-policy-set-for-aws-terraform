# This policy checks if resources of type 'aws_db_instance' have the 'username'
# attribute not set to 'admin' (default value)

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":              "rds-instance-default-admin-check",
	"message":                  "Attribute 'username' should not be 'admin' (default value) for AWS RDS DB Instance. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-25 for more details.",
	"resource_aws_db_instance": "aws_db_instance",
	"username_default_value":   "admin",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.username", const.username_default_value) is not const.username_default_value
	})
}

# Variables

db_instances = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_db_instance).resources
violations = get_violations(db_instances)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
