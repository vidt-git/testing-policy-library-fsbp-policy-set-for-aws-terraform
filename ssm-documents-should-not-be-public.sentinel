# This policy requires resources of type `aws_ssm_document` to not be public.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":               "ssm-documents-should-not-be-public",
	"message":                   "SSM documents should not be public. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/ssm-controls.html#ssm-4 for more details.",
	"resource_aws_ssm_document": "aws_ssm_document",
	"permissions":               "permissions",
	"account_ids":               "account_ids",
	"type":                      "type",
	"share":                     "Share",
	"all":                       "All",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		permissions = maps.get(res, "values." + const.permissions, [])
		if permissions is null or permissions is empty {
			return true
		}
		return maps.get(permissions, const.type, null) is const.share and
			maps.get(permissions, const.account_ids, []) is not const.all

	})
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_ssm_document).resources
violations = get_violations(resources)

summary = {
	"policy_name": "ssm-document-not-public",
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
