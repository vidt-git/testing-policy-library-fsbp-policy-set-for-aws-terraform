# This policy requires resources of type `aws_fsx_lustre_file_system` have the `copy_tags_to_backups` attributes set to true.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Import

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name": "fsx-lustre-copy-tags-to-backups",
	"message":     "Attributes 'copy_tags_to_backups' must be true for 'aws_fsx_lustre_file_system' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/fsx-controls.html#fsx-2 for more details.",
	"resource_aws_fsx_lustre_file_system": "aws_fsx_lustre_file_system",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.copy_tags_to_backups", false) is true
	})
}

# Variables

fsx_lustre_file_system_resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_fsx_lustre_file_system).resources
violations = get_violations(fsx_lustre_file_system_resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
