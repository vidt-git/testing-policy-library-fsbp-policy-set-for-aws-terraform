# This policy requires attribute 'backup_retention_period' to be greater than or equal to 7 for 'aws_neptune_cluster' resources

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Params

# backup_retention_period specifies the minimum days backups need to retained.
param backup_retention_period default 7

# Constants
const = {
	"policy_name":                  "neptune-cluster-automated-backups-enabled",
	"resource_aws_neptune_cluster": "aws_neptune_cluster",
}

# Functions
get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		return maps.get(res, "values.backup_retention_period", 1) >= backup_retention_period
	})
}

# Variables

aws_neptune_clusters = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_neptune_cluster).resources
violations = get_violations(aws_neptune_clusters)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        "Attribute 'backup_retention_period' must be greater than or equal to " + string(backup_retention_period) + " for 'aws_neptune_cluster' resources.Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/neptune-controls.html#neptune-5 for more details.",
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
