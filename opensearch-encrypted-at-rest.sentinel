# This policy checks if resources of type 'aws_opensearch_domain' have the 'enable' attribute set to true
# in the 'encrypt_at_rest' block

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                    "opensearch-encrypted-at-rest",
	"message":                        "Attribute 'enable' in 'encrypt_at_rest' should be true for AWS OpenSearch Domain. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/opensearch-controls.html#opensearch-1 for more details.",
	"resource_aws_opensearch_domain": "aws_opensearch_domain",
	"enabled":                        "enabled",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		encrypt_at_rest_values = maps.get(res, "values.encrypt_at_rest", null)
		if encrypt_at_rest_values is null {
			return false
		}
		return maps.get(encrypt_at_rest_values[0], const.enabled, false) is true
	})
}

# Variables

opensearch_domain_resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_opensearch_domain).resources
violations = get_violations(opensearch_domain_resources)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs
print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
