# This policy checks if the aws rds instances are publicly accessible

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":  "rds-instance-deployed-in-vpc",
	"message":      "Attribute 'db_subnet_group_name' should be present for aws_db_instance resource. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-18 for more details.",
	"rds_resource": "aws_db_instance",
}

# Functions

build_violation_object = func(res, message) {
	return {
		"address":        res.address,
		"module_address": res.module_address,
		"message":        message,
	}
}

# Variables

rds_resources = tf.plan(tfplan.planned_values.resources).type(const.rds_resource).resources
violations = collection.reject(rds_resources, func(res) {
	db_subnet_group_name_key = "values.db_subnet_group_name"
	return maps.get(res, db_subnet_group_name_key, "") is not empty
})

rds_violations = map violations as _, res {
	build_violation_object(res, const.message)
}

summary = {
	"policy_name": const.policy_name,
	"violations":  rds_violations,
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
