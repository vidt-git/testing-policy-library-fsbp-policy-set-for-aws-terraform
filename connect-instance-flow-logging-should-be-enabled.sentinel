# This policy requires resources of type `aws_connect_instance` have attribute `contact_flow_logs_enabled` should be true.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                   "connect-instance-flow-logging-should-be-enabled",
	"message":                       "Attribute 'contact_flow_logs_enabled' must be true for 'aws_connect_instance' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/connect-controls.html#connect-2 for more details.",
	"resource_aws_connect_instance": "aws_connect_instance",
	"contact_flow_logs_enabled":     "contact_flow_logs_enabled",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_connect_instance).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res.values, const.contact_flow_logs_enabled, false) is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
