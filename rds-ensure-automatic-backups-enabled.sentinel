# This policy requires resources of type `aws_db_instance` have attribute "backup_retention_period" set to integer value between (7, 35).

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":              "rds-ensure-automatic-backups-enabled",
	"message":                  "Attribute 'backup_retention_period' must be set to integer between (7, 35) for 'aws_db_instance' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-11 for more details.",
	"resource_aws_db_instance": "aws_db_instance",
	"backup_retention_period":  "backup_retention_period",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_db_instance).resources
violations = collection.reject(resources, func(res) {
	backup_retention_period_value = maps.get(res, "values.backup_retention_period", 0)
	return backup_retention_period_value >= 7 and backup_retention_period_value <= 35
})

summary = {
	"policy_name": "rds-ensure-automatic-backups-enabled",
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
