# This policy requires resources of type `aws_wafregional_web_acl` to have at least one rule or rule group.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                      "waf-regional-webacl-not-empty",
	"message":                          "AWS WAF Classic Regional web ACLs should have at least one rule or rule group. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/waf-controls.html#waf-4 for more details.",
	"resource_aws_wafregional_web_acl": "aws_wafregional_web_acl",
	"rule": "rule",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_wafregional_web_acl).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res, "values.rule", []) is not empty
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
