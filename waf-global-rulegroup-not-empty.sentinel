# This policy requires resources of type `aws_waf_rule_group` to have at least one rule.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                 "waf-global-rulegroup-not-empty",
	"message":                     "AWS WAF Classic global rule groups should have at least one rule. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/waf-controls.html#waf-7 for more details.",
	"resource_aws_waf_rule_group": "aws_waf_rule_group",
	"activated_rule":              "activated_rule",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_waf_rule_group).resources
violations = collection.reject(resources, func(res) {
	return maps.get(res.values, const.activated_rule, []) is not empty
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
