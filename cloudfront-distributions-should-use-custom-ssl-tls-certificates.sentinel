# This control checks whether 'aws_cloudfront_distribution' are using the default SSL/TLS certificate CloudFront provides.

# Copyright IBM Corp. 2024, 2025
# SPDX-License-Identifier: BUSL-1.1

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":                          "cloudfront-distributions-should-use-custom-ssl-tls-certificates",
	"resource_aws_cloudfront_distribution": "aws_cloudfront_distribution",
	"message": "'aws_cloudfront_distribution' are using the default SSL/TLS certificate CloudFront provides. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/cloudfront-controls.html#cloudfront-7 for more details.",
}

# Functions

get_violations = func(resources) {
	return collection.reject(resources, func(res) {
		viewer_certificate = maps.get(res.values, "viewer_certificate", [])
		if viewer_certificate is empty or viewer_certificate[0] is not defined {
			return true
		}
		default_certificate = maps.get(viewer_certificate[0], "cloudfront_default_certificate", false)
		return default_certificate is null or default_certificate is false
	})
}

# Variables

aws_cloudfront_distribution = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_cloudfront_distribution).resources
violations = get_violations(aws_cloudfront_distribution)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
